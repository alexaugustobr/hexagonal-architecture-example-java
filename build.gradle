plugins {
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.asciidoctor.convert' version '1.5.8'
    id 'java'
    id 'idea'
}

apply from: 'gradle/test-architecture.gradle'
apply from: 'gradle/test-acceptance.gradle'

group 'ddd.example.todo'
version '1.0.0-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
    }
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    def dddBitsVersion = '0.0.1'
    def lombokVersion = '1.18.12'

    implementation "io.hschwentner.dddbits:dddbits:${dddBitsVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-web'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // test
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // acceptanceTest
    def cucumberVersion = '5.6.0'
    acceptanceTestImplementation "io.cucumber:cucumber-java:${cucumberVersion}"
    acceptanceTestImplementation "io.cucumber:cucumber-junit:${cucumberVersion}"

    // architectureTest
    architectureTestImplementation "io.hschwentner.dddbits:dddbits:${dddBitsVersion}"
    architectureTestImplementation 'com.tngtech.archunit:archunit-junit4:0.13.1'
    architectureTestImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1'
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}
